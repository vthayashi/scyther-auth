/*
 * Modeled from ISO/IEC 9798; Cas Cremers, Dec. 2010 scyther model
 * Modeler: Victor Hayashi
 *
 * symmetric
 * three-pass
 * mutual
 *
 * Notes:
* - Agent A was replaced by TD (Trusted Device)
* - Agent B was replaced by BS (Bank Server)
* - Retired symmetric-role partnering function
* - Retired additional Text1, Text2, Text3, Text4 and Text5
* - Changed order of factors in messages 2 and 3 to be identical to specification
* - Added identifier (bs,td) as var/fresh Tickets and added secret claims
* - Retired Running and Commit claims
* - Replaced R in Nonces by N
* - Put NTD nonce out of symmetric cryptographic function in message 2
* - Simplified according to BAN Logic
*/

protocol skid3simplified(TD,BS)
{
	role TD
	{
		var NBS: Nonce;
		fresh NTD: Nonce;

		fresh bs: Ticket;
		var td: Ticket;

		recv_1(BS,TD, NBS);

		send_2(TD,BS, NTD, { NBS, bs }k(TD,BS) );
		recv_3(BS,TD, { NTD, NBS, td }k(TD,BS) );

		claim(TD,Alive);
		claim(TD,Weakagree);

		claim(TD,Secret,td);
		claim(TD,Secret,bs);
	}

	role BS
	{
		fresh NBS: Nonce;
		var NTD: Nonce;

		var bs: Ticket;
		fresh td: Ticket;

		send_1(BS,TD, NBS);
		recv_2(TD,BS, NTD, { NBS, bs }k(TD,BS) );

		send_3(BS,TD, { NTD, NBS, td }k(TD,BS) );

		claim(BS,Alive);
		claim(BS,Weakagree);
		
		claim(BS,Secret,td);
		claim(BS,Secret,bs);
	}
}

